renderers = get_option('renderers')
if 'auto' in renderers and get_option('auto_features').enabled()
	renderers = ['gles2', 'vulkan']
elif 'auto' in renderers and get_option('auto_features').disabled()
	renderers = []
endif

wlr_files += files(
	'matrix.c',
	'fx_stencilbuffer.c',
	'fx_renderer.c',
)

if 'gles2' in renderers or 'auto' in renderers
	egl = dependency('egl', required: 'gles2' in renderers)
	gbm = dependency('gbm', required: 'gles2' in renderers)
	glesv2 = dependency('glesv2', required: 'gles2' in renderers)

	if egl.found() and gbm.found() and glesv2.found()
		wlr_deps += [egl, gbm, glesv2]
		internal_features += { 'egl': true , 'gles2-renderer': true }
	endif
	subdir('gles2')
endif

